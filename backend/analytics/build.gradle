plugins {
	id 'io.freefair.lombok' version '5.1.1'
	id 'org.springframework.boot' version '2.3.3.RELEASE'
	id 'io.spring.dependency-management' version '1.0.10.RELEASE'
	id 'java'
	id 'com.github.jk1.dependency-license-report' version '1.13'
}

group = 'com.griddynamics.msd365fp.manual-review'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations.all {
    resolutionStrategy.force 'com.nimbusds:nimbus-jose-jwt:7.9', 'com.google.guava:guava:25.0-jre'
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

ext {
	set('azureVersion', '2.3.2')
    set('appInsightsVersion', '2.6.1')
}

dependencies {
	compile project(path:':model', configuration: 'default')
	compile project(path:':cosmos-utilities', configuration: 'default')
	compile project(path:':azure-graph-client', configuration: 'default')
	compile project(path:':dfp-auth-starter', configuration: 'default')

    implementation 'com.nimbusds:nimbus-jose-jwt:7.9'
    implementation 'com.google.guava:guava:25.0-jre'
    implementation 'io.github.resilience4j:resilience4j-spring-boot2:1.4.0'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.modelmapper:modelmapper:2.3.7'
    implementation 'org.springframework:spring-webflux'
	implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
    implementation 'org.springframework.boot:spring-boot-starter-mail'
    implementation 'org.springdoc:springdoc-openapi-ui:1.3.9'
    implementation 'org.springdoc:springdoc-openapi-webmvc-core:1.3.9'

//    Azure
	implementation 'com.microsoft.azure:azure-spring-boot-starter'
    implementation 'com.microsoft.azure:azure-cosmosdb-spring-boot-starter'
    implementation 'com.microsoft.azure:spring-cloud-azure-eventhubs-stream-binder:1.2.7'
    implementation 'com.microsoft.azure:azure-keyvault-secrets-spring-boot-starter'

	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
    testImplementation 'org.mockito:mockito-junit-jupiter:2.23.0'
	testImplementation 'org.springframework.security:spring-security-test'
}

dependencyManagement {
	imports {
		mavenBom "com.microsoft.azure:azure-spring-boot-bom:${azureVersion}"
	}
}

test {
	useJUnitPlatform()
}

task copyForArchiving(type: Copy) {
		dependsOn 'bootJar'
		from "$buildDir/libs", "$buildDir/resources/main"
		include "*.jar", "ApplicationInsights.json"
		into "$buildDir/toArchive"
		rename 'analytics-(.*).jar', 'app.jar'
		rename 'applicationinsights-agent-(.*).jar', 'applicationinsights-agent.jar'
}

task packageDist(type: Zip) {
		dependsOn 'copyForArchiving'
    archiveFileName = "target.zip"
    destinationDirectory = file("$buildDir/dist")

    from "$buildDir/toArchive"
}
